; definition of an entry in IDT
struc IDTEntry
	.offsetl resw 1
	.selector resw 1
	.ist resb 1
	.attribute resb 1
	.offsetm resw 1
	.offseth resd 1
	.reserved resd 1
endstruc

SECTION .text
USE64
interrupts:
; generate 256 handlers for each possible interrupts
; all interrupts will call the address stored in .handler
; the number of the interrupt will be passed by memory location 0x100000
.first:
	mov [.entry], byte 0
    jmp qword .handle
.second:
%assign i 1
%rep 255
	mov [.entry], byte i
    jmp qword .handle
%assign i i+1
%endrep
.handle:
    ; actual interrupt handling code
    ; first, save all the registers and the interrupt number on the stack
	push rbp
	push r15
	push r14
	push r13
	push r12
	push r11
	push r10
	push r9
	push r8
	push rsi
	push rdi
	push rdx
	push rcx
	push rbx
	push rax

	mov rsi, rsp
	push rsi
	mov rdi, qword [.entry]
	push rdi

    ; load segment registers with kernel data segment
    mov rax, gdt.kernel_data
    mov ds, rax
    mov es, rax
    mov fs, rax
    mov gs, rax

    ; call the handler procedure
	call qword [.handler]

	mov rax, gdt.user_data | 3 ; user data segment at lowest privilege
    mov ds, rax
    mov es, rax
    mov fs, rax
    mov gs, rax

	add rsp, 16 ; Skip interrupt code and reg pointer

    ; restore the registers
	pop rax
	pop rbx
	pop rcx
	pop rdx
	pop rdi
	pop rsi
	pop r8
	pop r9
	pop r10
	pop r11
	pop r12
	pop r13
	pop r14
	pop r15
	pop rbp

    iretq

; the address of the handler procedure
.handler: dq 0

; the IDT descriptor
idtr:
    dw (idt.end - idt) + 1
    dq idt

; actual IDT
idt:
; every interrupt is assigned to appropriate offset in the code generated by the macro above
%assign i 0

;Below syscall
%rep 128
	istruc IDTEntry
		at IDTEntry.offsetl, dw interrupts+(interrupts.second-interrupts.first)*i
		at IDTEntry.selector, dw gdt.kernel_code
		at IDTEntry.ist, db 0
		at IDTEntry.attribute, db attrib.present | attrib.interrupt64
		at IDTEntry.offsetm, dw 0
		at IDTEntry.offseth, dd 0
		at IDTEntry.reserved, dd 0
	iend
%assign i i+1
%endrep

;Syscall
istruc IDTEntry
	at IDTEntry.offsetl, dw interrupts+(interrupts.second-interrupts.first)*i
	at IDTEntry.selector, dw gdt.kernel_code
	at IDTEntry.ist, db 0
	at IDTEntry.attribute, db attrib.present | attrib.ring3 | attrib.interrupt64
	at IDTEntry.offsetm, dw 0
	at IDTEntry.offseth, dd 0
	at IDTEntry.reserved, dd 0
iend
%assign i i+1

;Above syscall
%rep 127
	istruc IDTEntry
		at IDTEntry.offsetl, dw interrupts+(interrupts.second-interrupts.first)*i
		at IDTEntry.selector, dw gdt.kernel_code
		at IDTEntry.ist, db 0
		at IDTEntry.attribute, db attrib.present | attrib.interrupt64
		at IDTEntry.offsetm, dw 0
		at IDTEntry.offseth, dd 0
		at IDTEntry.reserved, dd 0
	iend
%assign i i+1
%endrep
.end:
